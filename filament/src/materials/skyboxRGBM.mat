material {
    name : "Skybox RGBM",
    parameters : [
        {
           type : bool,
           name : showSun
        },
        {
           type : samplerCubemap,
           name : skybox
        }
    ],
    variables : [
         eyeDirection
    ],
    vertexDomain : device,
    depthWrite : false,
    shadingModel : unlit,
    variantFilter : [ skinning ],
    culling: none
}

fragment {
    void material(inout MaterialInputs material) {
        prepareMaterial(material);
        vec3 sky = decodeRGBM(texture(materialParams_skybox, variable_eyeDirection.xyz));
        sky *= iblLuminance;
        if (materialParams.showSun && sun.w >= 0.0f) {
            vec3 direction = normalize(variable_eyeDirection.xyz);
            vec3 sun = lightColorIntensity.rgb * lightColorIntensity.a;
            float cosAngle = dot(direction, lightDirection);
            float x = (cosAngle - sun.x) * sun.z;
            float gradient = pow(1.0 - saturate(x), sun.w);
            sky += sun * gradient;
        }
        material.baseColor = vec4(sky, 1.0);
    }
}

vertex {
    void materialVertex(inout MaterialVertexInputs material) {
        float3 p = getPosition().xyz;
        float3 unprojected = mulMat4x4Float3(getViewFromClipMatrix(), p).xyz;
        material.eyeDirection.xyz = mulMat3x3Float3(getWorldFromViewMatrix(), unprojected);
    }
}

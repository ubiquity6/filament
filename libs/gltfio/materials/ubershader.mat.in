material {
    name : ubershader,
    requires : [ uv0, uv1, color ],
    shadingModel : ${SHADINGMODEL},
    blending : ${BLENDING},
    depthWrite : true,
    doubleSided : false,
    flipUV : false,
    parameters : [

        { type : float3, name : specularFactor },
        { type : float, name : glossinessFactor },

        // Base Color
        { type : int, name : baseColorIndex },
        { type : float4, name : baseColorFactor },
        { type : sampler2d, name : baseColorMap },
        { type : mat3, name : baseColorUvMatrix },

        // Metallic-Roughness Map
        { type : int, name : metallicRoughnessIndex },
        { type : float, name : metallicFactor },
        { type : float, name : roughnessFactor },
        { type : sampler2d, name : metallicRoughnessMap },
        { type : mat3, name : metallicRoughnessUvMatrix },

        // Normal Map
        { type : int, name : normalIndex },
        { type : float, name : normalScale },
        { type : sampler2d, name : normalMap },
        { type : mat3, name : normalUvMatrix },

        // Ambient Occlusion
        { type : int, name : aoIndex },
        { type : float, name : aoStrength },
        { type : sampler2d, name : occlusionMap },
        { type : mat3, name : occlusionUvMatrix },

        // Emissive Map
        { type : int, name : emissiveIndex },
        { type : float3, name : emissiveFactor },
        { type : sampler2d, name : emissiveMap },
        { type : mat3, name : emissiveUvMatrix }
    ],
}

fragment {
    void material(inout MaterialInputs material) {
        float2 uvs[2];
        uvs[0] = getUV0();
        uvs[1] = getUV1();

        #if !defined(SHADING_MODEL_UNLIT)
            if (normalIndex > -1) {
                float2 uv = uvs[normalIndex];
                uv = (vec3(uv, 1.0) * normalUvMatrix).xy;
                material.normal = texture(materialParams_normalMap, uv).xyz * 2.0 - 1.0;
                material.normal.xy *= normalScale;
            }
        #endif

        prepareMaterial(material);
        material.baseColor = baseColorFactor;

        if (baseColorIndex > -1) {
            float2 uv = uvs[baseColorIndex];
            uv = (vec3(uv, 1.0) * baseColorUvMatrix).xy;
            material.baseColor *= texture(materialParams_baseColorMap, uv);
        }

        #if defined(BLEND_MODE_TRANSPARENT)
            material.baseColor.rgb *= material.baseColor.a;
        #endif

        material.baseColor *= getColor();

        #if !defined(SHADING_MODEL_UNLIT)

            #if defined(SHADING_MODEL_LIT)
                material.roughness = roughnessFactor;
                material.metallic = metallicFactor;
            #endif

            material.emissive.rgb = emissiveFactor.rgb;
            material.emissive.a = 3.0;

            #if defined(SHADING_MODEL_SPECULAR_GLOSSINESS)
                material.glossiness = glossinessFactor;
                material.specularColor = specularFactor;
            #endif

            if (metallicRoughnessIndex > -1) {
                float2 uv = uvs[metallicRoughnessIndex];
                uv = (vec3(uv, 1.0) * metallicRoughnessUvMatrix).xy;

                #if defined(SHADING_MODEL_SPECULAR_GLOSSINESS)
                    vec4 sg = texture(materialParams_metallicRoughnessMap, uv);
                    material.specularColor *= sg.rgb;
                    material.glossiness *= sg.a;
                #else
                    vec4 mr = texture(materialParams_metallicRoughnessMap, uv);
                    material.roughness *= mr.g;
                    material.metallic *= mr.b;
                #endif
            }

            if (aoIndex > -1) {
                float2 uv = uvs[aoIndex];
                uv = (vec3(uv, 1.0) * occlusionUvMatrix).xy;
                material.ambientOcclusion = texture(materialParams_occlusionMap, uv).r *
                        aoStrength;
            }
            if (emissiveIndex > -1) {
                float2 uv = uvs[emissiveIndex];
                uv = (vec3(uv, 1.0) * emissiveUvMatrix).xy;
                material.emissive.rgb *= texture(materialParams_emissiveMap, uv).rgb;
            }
        #endif
    }
}
